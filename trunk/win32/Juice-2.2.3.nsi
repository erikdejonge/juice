; Please edit file with windows line feeds so that it can be edited
; within the windows enviroment. -- sgrayban
;
; Script generated by the HM NIS Edit Script Wizard.


BGGradient 000000 FEF42 FFFFFF
XPStyle on

;; These are set to default values (NSIS v2.02) so we
;; probably don't need them.
SetDateSave on
SetDatablockOptimize on
CRCCheck on
SilentInstall normal


; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Juice"
!define PRODUCT_VERSION "2.2.3"
!define PRODUCT_PUBLISHER "Juice Team"
!define PRODUCT_WEB_SITE "http://juicereceiver.sourceforge.net/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Juice.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\icons_status\installer_icon.ico"
!define MUI_UNICON "..\icons_status\installer_icon.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\gpl.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\Juice.exe"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\readme.txt"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Juice-${PRODUCT_VERSION}-Setup.exe"
InstallDir "$PROGRAMFILES\Juice"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "-Juice" SEC01
  FindProcDLL::FindProc "Juice.exe"
  StrCmp $R0 "1" concurrentproc
  FindProcDLL::FindProc "iPodder.exe"
  StrCmp $R0 "0" readytowrite

  concurrentproc:

  MessageBox MB_RETRYCANCEL "Another copy of Juice is running.  Please quit Juice before continuing by selecting File -> Quit or right-clicking on the Lemon icon in the system tray and selecting Quit." IDCANCEL cancel


  FindProcDLL::FindProc "Juice.exe"
  StrCmp $R0 "1" concurrentproc
  FindProcDLL::FindProc "iPodder.exe"
  StrCmp $R0 "1" concurrentproc
  StrCmp $R0 "0" readytowrite

  cancel:

  Abort "Can't install"

  readytowrite:

  SetOutPath "$INSTDIR"

  ; don't overwrite existing favorites
  SetOverwrite off
  File "..\favorites.txt"
  ; only overwrite GPL if current installing one is newer
  SetOverwrite ifnewer
  File "..\gpl.txt"
  ; everything else in dist tree overwrites
  SetOverwrite on
  !include ${INSTALL_LIST}
  ;File /r /x ..\dist\favorites.txt /x ..\dist\gpl.txt ..\dist\*.*
  CreateDirectory "$SMPROGRAMS\Juice"
  CreateShortCut "$SMPROGRAMS\Juice\Juice.lnk" "$INSTDIR\Juice.exe"
  CreateShortCut "$DESKTOP\Juice.lnk" "$INSTDIR\Juice.exe"
SectionEnd

Section "Desktop shortcut"
  CreateShortCut "$DESKTOP\Juice.lnk" "$INSTDIR\Juice.exe"
SectionEnd

Section /o "Add to Startup Group"
  CreateShortCut "$SMSTARTUP\Juice.lnk" "$INSTDIR\Juice.exe"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Juice\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Juice\Uninstall.lnk" "$INSTDIR\uninst.exe"
  CreateShortCut "$SMPROGRAMS\Juice\UserGuide.lnk" "$INSTDIR\docs\JuiceUserGuide.html"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\Juice.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\Juice.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

  ; File type registrations
  WriteRegStr HKCR "Applications\Juice.exe" "" "Juice"
  WriteRegStr HKCR "Applications\Juice.exe\shell\open\command" "" "$\"$INSTDIR\Juice.exe$\" --open $\"%1$\""
  WriteRegStr HKCR "Applications\Juice.exe\DefaultIcon" "" "$INSTDIR\icons_status\application.ico,1"
  WriteRegStr HKCR "Juice.rss" "" "Podcast Subscription File"
  WriteRegStr HKCR "Juice.rss\shell\open\command" "" "$\"$INSTDIR\Juice.exe$\" --add-feed-from-rss $\"%1$\""
  WriteRegStr HKCR ".rss\OpenWithList\Juice.exe" "" ""
  WriteRegBin HKCR ".rss\OpenWithProgIds" "Juice.rss" 0
  WriteRegStr HKCR "Juice.pcast" "" "Podcast Subscription File"
  WriteRegStr HKCR "Juice.pcast\shell\open\command" "" "$\"$INSTDIR\Juice.exe$\" --add-feed-from-pcast $\"%1$\""
  WriteRegStr HKCR ".pcast\OpenWithList\Juice.exe" "" ""
  WriteRegBin HKCR ".pcast\OpenWithProgIds" "Juice.pcast" 0

SectionEnd

Function .onInit

  ReadRegStr $R0 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "UninstallString"

  ReadRegStr $R2 HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}" "DisplayVersion"
  ; For future OS detection using the General.nsh macro file
  ; Need version.dll and General.nsh from the nsis_tools directory.
  ; Read the README.txt in the win32/nsis_tools/
  ; -- sgrayban
  ; !include "General.nsh"
  ; !define winNT
  ; !insertmacro CHECKWINVER

FunctionEnd

Function .onInstSuccess

  StrCmp $R0 "" done
  StrCmp $R2 "1.0.0" onePointOh
  StrCmp $R2 "1.1" onePointEx
  StrCmp $R2 "1.1.2" onePointEx
  StrCmp $R2 "1.1.3" onePointEx
  StrCmp $R2 "1.1.4" onePointEx

  ;Assume done for all unknown release numbers.
  Goto done
  
  onePointOh:  
  ; They're upgrading from 1.0
  
  ; $R0 is the 1.0 uninstall string.  It should look like this: 1.0INSTDIR\uninst.exe
  ; $R1 is the old installation directory.
  StrCpy $R1 $R0 -11
  
  ExpandEnvStrings $0 "%APPDATA%"

  StrCmp $0 "%APPDATA%" 0 +2
    ExpandEnvStrings $0 "%HOMEDRIVE%%HOMEPATH%\Application Data"

  IfFileExists $0\*.* 0 done
  IfFileExists $0\iPodder\*.* +2 0
    CreateDirectory $0\iPodder

  ;copy any old configuration
  IfFileExists $0\iPodder\history.txt +3 0
  IfFileExists $R1\history.txt 0 +2
  CopyFiles $R1\history.txt $0\iPodder

  IfFileExists $0\iPodder\schedule.txt +3 0
  IfFileExists $R1\schedule.txt 0 +2
  CopyFiles $R1\schedule.txt $0\iPodder
  
  IfFileExists $0\iPodder\favorites.txt +3 0
  IfFileExists $R1\favorites.txt 0 +2
  CopyFiles $R1\favorites.txt $0\iPodder

  onePointEx:
  ;They're upgrading from 1.1 - 1.1.4.  Backup the state db in case they want to go back.

  ExpandEnvStrings $0 "%APPDATA%"

  StrCmp $0 "%APPDATA%" 0 +2
    ExpandEnvStrings $0 "%HOMEDRIVE%%HOMEPATH%\Application Data"

  IfFileExists $0\iPodder\iPodder.db 0 +3
    CopyFiles $0\iPodder\iPodder.db $0\iPodder\iPodder.db-$R2
    ;MessageBox MB_OK "iPodder $R2 data backed up to $0\iPodder\iPodder.db-$R2"

  done:

FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall

  # This include has all the deletes for the staged build
!include ${UNINSTALL_LIST}

  # Other files installed by the installer
  Delete $INSTDIR\gpl.txt
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  # DeleteFile $INSTDIR\favorites.txt # we don't delete this
  Delete $INSTDIR\uninst.exe
  
  # this won't usually work, due to non-deleted favorites
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCR "Applications\Juice.exe"
  DeleteRegKey HKCR "Juice.rss"
  DeleteRegKey HKCR "Juice.pcast"
  DeleteRegKey HKCR ".rss\OpenWithList\Juice.exe"
  DeleteRegValue HKCR ".rss\OpenWithProgIds" "Juice.rss"
  DeleteRegKey HKCR ".pcast\OpenWithList\Juice.exe"
  DeleteRegValue HKCR ".pcast\OpenWithProgIds" "Juice.pcast"

  SetAutoClose true
SectionEnd
